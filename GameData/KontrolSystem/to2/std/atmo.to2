use { Vessel, active_vessel } from ksp::vessel
use { trigger_staging } from std::staging
use { clamp, max, acos_deg } from core::math
use { sleep, wait_until } from ksp::game
use { circularize_orbit } from std::maneuvers
use { exec_next_node } from std::vac
use { CONSOLE } from ksp::console

/// Automatically launch a rocket from an atmosphere to a circular orbit.
pub fn atmo_launch(target_apoapsis: float, heading: float) -> Result<Unit, string> = {
    const vessel = active_vessel()?

    vessel.actions.sas = false
    vessel.actions.rcs = false
    vessel.actions.light = true

    atmo_launch_ascent(vessel, target_apoapsis, heading)

    
    const (delta_v, UT) = circularize_orbit(vessel.orbit)?

    vessel.maneuver.add_burn_vector(UT, delta_v)?

    exec_next_node(vessel)?

    vessel.actions.deploy_fairings()

    sleep(2)
    
    vessel.actions.panels = true
    vessel.actions.radiators = true
    vessel.actions.antennas = true

    vessel.actions.sas = true
    vessel.actions.rcs = false
}

/// Perform a rocket launch ascent from an atmosphere.
///
/// Note: The rocket will not end up in a stable orbit and most likely crash if no further action
/// is taken.
pub fn atmo_launch_ascent(vessel: Vessel, target_apoapsis: float, heading: float) -> Unit = {
    CONSOLE.print_line("=== atmo_launch_ascent ===")
    const console_row = CONSOLE.cursor_row
    CONSOLE.move_cursor(console_row + 2, 0)
    
 	// Starting/ending height of gravity turn
    const launch_gt0 = vessel.main_body.atmosphere_depth * 0.007
    const launch_gt1 = vessel.main_body.atmosphere_depth * 0.6

    const steering_manager = vessel.manage_steering(fn() -> {
        const gtPct = clamp((vessel.altitude - launch_gt0) / (launch_gt1 - launch_gt0), 0, 1)
        const pitch = acos_deg(gtPct)

        CONSOLE.print_at(console_row, 0, "Target pitch: " + pitch.to_string())

        vessel.heading_direction(heading, pitch, -90)
    })
    
    const throttle_manager = vessel.manage_throttle(fn() -> {
        const atmPct = vessel.altitude / (vessel.main_body.atmosphere_depth + 1)
        const spd = vessel.air_speed
        const cutoff = 200 + (400 * max(0, atmPct * 3))

        const throttle = if(spd > cutoff) {
            1.0 - max(0.1, ((spd - cutoff) / cutoff))
        } else {
            const apoPercent = vessel.orbit.apoapsis / target_apoapsis
            const apoCompensation = if(apoPercent > 0.9) (apoPercent - 0.9) * 10 else 0.0
            1.0 - clamp(apoCompensation, 0, 0.5)
        }
        CONSOLE.print_at(console_row + 1, 0, "Target throttle: " + throttle.to_string() )

        throttle
    })

    sleep(1.0)

    CONSOLE.print_line("Launch: Trigger initial stage")
    vessel.stage.next()

    while(vessel.orbit.apoapsis < target_apoapsis) {
        trigger_staging(vessel)
        sleep(0.05)
    }

    throttle_manager.release()
    steering_manager.set_direction_provider(fn() -> vessel.prograde)

    wait_until(fn() -> vessel.altitude > vessel.main_body.atmosphere_depth * 0.9)

    steering_manager.release()
}